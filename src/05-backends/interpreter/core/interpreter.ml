module Error = Utils.Error
module Ast = Language.Ast
module Const = Language.Const
module Context = Language.Context
module PrettyPrint = Language.PrettyPrint

module Types = struct
  type computation_redex =
    | Match
    | ApplyFun
    | DoReturn
    | DoOp
    | Delay
    | Box
    | Unbox
    | HandleReturn
    | HandleOp
    | DefaultOp

  type computation_reduction =
    | DoCtx of computation_reduction
    | HandleCtx of computation_reduction
    | ComputationRedex of computation_redex

  type step_label = ComputationReduction of computation_reduction | Return
end

module Make (GS : Language.GradeSystem.S) = struct
  module Grades = GS
  module Graded = Ast.Graded (GS)

  (* The interpreter has no use for anything but the accumulated resource grade
     itself, so its contexts record just that. *)
  module Elapsed = struct
    module Grade = struct
      type t = Graded.rho

      let one = Ast.RhoConst GS.R.one
      let mul rho rho' = Ast.RhoAdd (rho, rho')
    end

    type t = Graded.rho

    let grade rho = rho
  end

  (* Operation-case barriers are a typing device; the interpreter builds none. *)
  module Barrier = struct
    type t = unit
  end

  module ContextHolderModule =
    Context.Make (Ast.Variable) (Map.Make (Ast.Variable)) (Elapsed) (Barrier)

  module P = Primitives.Make (GS)
  include Types

  (* The state holds each resource with the resource grade it was boxed at,
     interleaved with the resource grades that have passed; operations are
     graded by effect grades. *)
  type evaluation_environment = {
    state : (Graded.rho * Graded.expression) ContextHolderModule.t;
    variables : Graded.expression ContextHolderModule.t;
    builtin_functions :
      (Graded.expression -> Graded.computation) ContextHolderModule.t;
    resource_counter : int;
    op_signatures : Graded.eps Ast.OpNameMap.t;
    op_defaults : Graded.abstraction Ast.OpNameMap.t;
  }

  let initial_environment =
    {
      state = ContextHolderModule.empty;
      variables = ContextHolderModule.empty;
      builtin_functions = ContextHolderModule.empty;
      resource_counter = 0;
      op_signatures = Ast.OpNameMap.empty;
      op_defaults = Ast.OpNameMap.empty;
    }

  exception PatternMismatch

  (** [rho_of_eps eps] is the resource grade [∣eps∣] of the effect grade [eps]
      of an operation signature, which has no parameters. *)
  let rec rho_of_eps = function
    | Ast.EpsConst c -> Ast.RhoConst (GS.map c)
    | Ast.EpsAdd (eps, eps') -> Ast.RhoAdd (rho_of_eps eps, rho_of_eps eps')
    | Ast.EpsParam _ | Ast.EpsRigid _ ->
        Error.runtime "internal: grade parameter in an operation signature"

  let rec eval_tuple (env : evaluation_environment) (expr : _ Ast.expression) =
    match expr.it with
    | Ast.Annotated (expr', _) -> eval_tuple env expr'
    | Ast.Tuple exprs -> exprs
    | Ast.Var x ->
        eval_tuple env (ContextHolderModule.find_variable x env.variables)
    | _ ->
        Error.runtime "Tuple expected but got %t"
          (PrettyPrint.print_expression (module GS.R) expr)

  let rec eval_variant (env : evaluation_environment) (expr : _ Ast.expression)
      =
    match expr.it with
    | Ast.Annotated (expr', _) -> eval_variant env expr'
    | Ast.Variant (lbl, arg) -> (lbl, arg)
    | Ast.Var x ->
        eval_variant env (ContextHolderModule.find_variable x env.variables)
    | _ ->
        Error.runtime "Variant expected but got %t"
          (PrettyPrint.print_expression (module GS.R) expr)

  let rec eval_const (env : evaluation_environment) (expr : _ Ast.expression) =
    match expr.it with
    | Ast.Annotated (expr', _) -> eval_const env expr'
    | Ast.Const c -> c
    | Ast.Var x ->
        eval_const env (ContextHolderModule.find_variable x env.variables)
    | _ ->
        Error.runtime "Const expected but got %t"
          (PrettyPrint.print_expression (module GS.R) expr)

  let rec match_pattern_with_expression env (pat : _ Ast.pattern) expr =
    match pat.it with
    | Ast.PVar x -> Ast.VariableMap.singleton x expr
    | Ast.PAnnotated (pat, _) -> match_pattern_with_expression env pat expr
    | Ast.PAs (pat, x) ->
        let subst = match_pattern_with_expression env pat expr in
        Ast.VariableMap.add x expr subst
    | Ast.PTuple pats ->
        let exprs = eval_tuple env expr in
        List.fold_left2
          (fun subst pat expr ->
            let subst' = match_pattern_with_expression env pat expr in
            Ast.VariableMap.union (fun _ _ _ -> assert false) subst subst')
          Ast.VariableMap.empty pats exprs
    | Ast.PVariant (label, pat) -> (
        match (pat, eval_variant env expr) with
        | None, (label', None) when label = label' -> Ast.VariableMap.empty
        | Some pat, (label', Some expr) when label = label' ->
            match_pattern_with_expression env pat expr
        | _, _ -> raise PatternMismatch)
    | Ast.PConst c when Const.equal c (eval_const env expr) ->
        Ast.VariableMap.empty
    | Ast.PNonbinding -> Ast.VariableMap.empty
    | _ -> raise PatternMismatch

  let rec remove_pattern_bound_variables subst (pat : _ Ast.pattern) =
    match pat.it with
    | Ast.PVar x -> Ast.VariableMap.remove x subst
    | Ast.PAnnotated (pat, _) -> remove_pattern_bound_variables subst pat
    | Ast.PAs (pat, x) ->
        let subst = remove_pattern_bound_variables subst pat in
        Ast.VariableMap.remove x subst
    | Ast.PTuple pats ->
        List.fold_left remove_pattern_bound_variables subst pats
    | Ast.PVariant (_, None) -> subst
    | Ast.PVariant (_, Some pat) -> remove_pattern_bound_variables subst pat
    | Ast.PConst _ -> subst
    | Ast.PNonbinding -> subst

  (* Refreshing rebuilds the pattern, but every node keeps the span it was
     written at: the variables change, the source they came from does not. *)
  let rec refresh_pattern (pat : _ Ast.pattern) =
    match pat.it with
    | Ast.PVar x ->
        let x' = Ast.Variable.refresh x in
        ({ pat with it = Ast.PVar x' }, [ (x, x') ])
    | Ast.PAnnotated (pat', _) -> refresh_pattern pat'
    | Ast.PAs (pat', x) ->
        let pat'', vars = refresh_pattern pat' in
        let x' = Ast.Variable.refresh x in
        ({ pat with it = Ast.PAs (pat'', x') }, (x, x') :: vars)
    | Ast.PTuple pats ->
        let fold pat' (pats', vars) =
          let pat'', vars' = refresh_pattern pat' in
          (pat'' :: pats', vars' @ vars)
        in
        let pats', vars = List.fold_right fold pats ([], []) in
        ({ pat with it = Ast.PTuple pats' }, vars)
    | Ast.PVariant (lbl, Some pat') ->
        let pat'', vars = refresh_pattern pat' in
        ({ pat with it = Ast.PVariant (lbl, Some pat'') }, vars)
    | Ast.PVariant (_, None) | Ast.PConst _ | Ast.PNonbinding -> (pat, [])

  (** | Ast.Handler ((y, ret_case), op_cases) -> let y' = Ast.Variable.refresh y
      in let ret_case' = refresh_computation ((y, y') :: vars) ret_case in let
      op_cases' = Ast.OpNameMap.map (fun (x, k, op_case) -> let x' =
      Ast.Variable.refresh x in let k' = Ast.Variable.refresh k in let op_case'
      = refresh_computation ((x, x') :: (k, k') :: vars) op_case in (x', k',
      op_case')) op_cases in Ast.Handler ((y', ret_case'), op_cases') *)
  let rec refresh_expression vars (expr : _ Ast.expression) =
    match expr.it with
    | Ast.Var x -> (
        match List.assoc_opt x vars with
        | None -> expr
        | Some x' -> { expr with it = Ast.Var x' })
    | Ast.Const _ -> expr
    | Ast.Annotated (e, ty) ->
        { expr with it = Ast.Annotated (refresh_expression vars e, ty) }
    | Ast.Tuple exprs ->
        { expr with it = Ast.Tuple (List.map (refresh_expression vars) exprs) }
    | Ast.Variant (label, arg) ->
        {
          expr with
          it = Ast.Variant (label, Option.map (refresh_expression vars) arg);
        }
    | Ast.Lambda abs ->
        { expr with it = Ast.Lambda (refresh_abstraction vars abs) }
    | Ast.PureLambda abs ->
        { expr with it = Ast.PureLambda (refresh_abstraction vars abs) }
    | Ast.RecLambda (x, abs) ->
        let x' = Ast.Variable.refresh x in
        {
          expr with
          it = Ast.RecLambda (x', refresh_abstraction ((x, x') :: vars) abs);
        }
    | Ast.Handler (ret_case, op_cases) ->
        let ret_case' = refresh_abstraction vars ret_case in
        let op_cases' =
          Ast.OpNameMap.map
            (fun op_case -> refresh_abstraction vars op_case)
            op_cases
        in
        { expr with it = Ast.Handler (ret_case', op_cases') }

  and refresh_computation vars (comp : _ Ast.computation) =
    match comp.it with
    | Ast.Return expr ->
        { comp with it = Ast.Return (refresh_expression vars expr) }
    | Ast.Do (c, abs) ->
        {
          comp with
          it = Ast.Do (refresh_computation vars c, refresh_abstraction vars abs);
        }
    | Ast.Match (expr, cases) ->
        {
          comp with
          it =
            Ast.Match
              ( refresh_expression vars expr,
                List.map (refresh_abstraction vars) cases );
        }
    | Ast.Apply (expr1, expr2) ->
        {
          comp with
          it =
            Ast.Apply
              (refresh_expression vars expr1, refresh_expression vars expr2);
        }
    | Ast.Delay (n, c) ->
        { comp with it = Ast.Delay (n, refresh_computation vars c) }
    | Ast.Box (rho, e, abs) ->
        let e' = refresh_expression vars e in
        let abs' = refresh_abstraction vars abs in
        { comp with it = Ast.Box (rho, e', abs') }
    | Ast.Unbox (e, abs) ->
        let e' = refresh_expression vars e in
        let abs' = refresh_abstraction vars abs in
        { comp with it = Ast.Unbox (e', abs') }
    | Ast.Perform (op, e, abs) ->
        let e' = refresh_expression vars e in
        let abs' = refresh_abstraction vars abs in
        { comp with it = Ast.Perform (op, e', abs') }
    | Ast.Handle (c, h) ->
        let c' = refresh_computation vars c in
        let h' = refresh_expression vars h in
        { comp with it = Ast.Handle (c', h') }

  and refresh_abstraction vars (pat, comp) =
    let pat', vars' = refresh_pattern pat in
    (pat', refresh_computation (vars @ vars') comp)

  (* A substituted variable takes the span of the value bound to it; every
     other node keeps its own. *)
  let rec substitute_expression subst (expr : _ Ast.expression) =
    match expr.it with
    | Ast.Var x -> (
        match Ast.VariableMap.find_opt x subst with
        | None -> expr
        | Some expr' -> expr')
    | Ast.Const _ -> expr
    | Ast.Annotated (e, ty) ->
        { expr with it = Ast.Annotated (substitute_expression subst e, ty) }
    | Ast.Tuple exprs ->
        {
          expr with
          it = Ast.Tuple (List.map (substitute_expression subst) exprs);
        }
    | Ast.Variant (label, arg) ->
        {
          expr with
          it = Ast.Variant (label, Option.map (substitute_expression subst) arg);
        }
    | Ast.Lambda abs ->
        { expr with it = Ast.Lambda (substitute_abstraction subst abs) }
    | Ast.PureLambda abs ->
        { expr with it = Ast.PureLambda (substitute_abstraction subst abs) }
    | Ast.RecLambda (x, abs) ->
        { expr with it = Ast.RecLambda (x, substitute_abstraction subst abs) }
    | Ast.Handler (ret_case, op_cases) ->
        {
          expr with
          it =
            Ast.Handler
              ( substitute_abstraction subst ret_case,
                Ast.OpNameMap.map
                  (fun op_case -> substitute_abstraction subst op_case)
                  op_cases );
        }

  and substitute_computation subst (comp : _ Ast.computation) =
    match comp.it with
    | Ast.Return expr ->
        { comp with it = Ast.Return (substitute_expression subst expr) }
    | Ast.Do (c, abs) ->
        {
          comp with
          it =
            Ast.Do
              (substitute_computation subst c, substitute_abstraction subst abs);
        }
    | Ast.Match (expr, cases) ->
        {
          comp with
          it =
            Ast.Match
              ( substitute_expression subst expr,
                List.map (substitute_abstraction subst) cases );
        }
    | Ast.Apply (expr1, expr2) ->
        {
          comp with
          it =
            Ast.Apply
              ( substitute_expression subst expr1,
                substitute_expression subst expr2 );
        }
    | Ast.Delay (n, c) ->
        { comp with it = Ast.Delay (n, substitute_computation subst c) }
    | Ast.Box (rho, e, abs) ->
        {
          comp with
          it =
            Ast.Box
              ( rho,
                substitute_expression subst e,
                substitute_abstraction subst abs );
        }
    | Ast.Unbox (e, abs) ->
        {
          comp with
          it =
            Ast.Unbox
              (substitute_expression subst e, substitute_abstraction subst abs);
        }
    | Ast.Perform (op, e, abs) ->
        {
          comp with
          it =
            Ast.Perform
              ( op,
                substitute_expression subst e,
                substitute_abstraction subst abs );
        }
    | Ast.Handle (c, h) ->
        {
          comp with
          it =
            Ast.Handle
              (substitute_computation subst c, substitute_expression subst h);
        }

  and substitute_abstraction subst (pat, comp) =
    let subst' = remove_pattern_bound_variables subst pat in
    (pat, substitute_computation subst' comp)

  let substitute subst comp =
    let subst = Ast.VariableMap.map (refresh_expression []) subst in
    substitute_computation subst comp

  let rec eval_function env (expr : _ Ast.expression) =
    match expr.it with
    | Ast.Annotated (expr', _) -> eval_function env expr'
    | Ast.Lambda (pat, comp) ->
        fun arg ->
          let subst = match_pattern_with_expression env pat arg in
          substitute subst comp
    | Ast.PureLambda (pat, comp) ->
        fun arg ->
          let subst = match_pattern_with_expression env pat arg in
          substitute subst comp
    | Ast.RecLambda (f, (pat, comp)) ->
        fun arg ->
          let subst =
            match_pattern_with_expression env pat arg
            |> Ast.VariableMap.add f expr
          in
          substitute subst comp
    | Ast.Var x -> (
        match ContextHolderModule.find_variable_opt x env.variables with
        | Some expr' -> eval_function env expr'
        | None -> ContextHolderModule.find_variable x env.builtin_functions)
    | _ ->
        Error.runtime "Function expected but got %t"
          (PrettyPrint.print_expression (module GS.R) expr)

  let rec eval_handler env (expr : _ Ast.expression) =
    match expr.it with
    | Ast.Annotated (expr', _) -> eval_handler env expr'
    | Ast.Handler (ret_case, op_cases) -> (ret_case, op_cases)
    | Ast.Var x -> (
        match ContextHolderModule.find_variable_opt x env.variables with
        | Some expr' -> eval_handler env expr'
        | None ->
            Error.runtime
              "Handler expected but did not find it from environment")
    | _ ->
        Error.runtime "Handler expected but got %t"
          (PrettyPrint.print_expression (module GS.R) expr)

  let step_in_context step env redCtx ctx term =
    let terms' = step env term in
    List.map
      (fun (env, red, term') -> (env, redCtx red, fun () -> ctx (term' ())))
      terms'

  (* Every computation the interpreter builds is a contraction of the redex it
     is reducing, so it is reported at the redex's span. *)
  let rec step_computation env (comp : _ Ast.computation) =
    let at = comp.at in
    match comp.it with
    | Ast.Return _ -> []
    | Ast.Match (expr, cases) ->
        let rec find_case = function
          | (env, pat, comp) :: cases -> (
              match match_pattern_with_expression env pat expr with
              | subst ->
                  [
                    ( env,
                      ComputationRedex Match,
                      fun () -> substitute subst comp );
                  ]
              | exception PatternMismatch -> find_case cases)
          | [] -> []
        in
        let cases' =
          List.map
            (fun (pat, comp) ->
              let pat', vars = refresh_pattern pat in
              let comp' = refresh_computation vars comp in
              (env, pat', comp'))
            cases
        in
        find_case cases'
    | Ast.Apply (expr1, expr2) ->
        let f = eval_function env expr1 in
        [ (env, ComputationRedex ApplyFun, fun () -> f expr2) ]
    | Ast.Do (comp1, comp2) -> (
        let comps1' =
          step_in_context step_computation env
            (fun red -> DoCtx red)
            (fun comp1' -> Ast.located at (Ast.Do (comp1', comp2)))
            comp1
        in
        match comp1.it with
        | Ast.Return expr ->
            let pat, comp2' = comp2 in
            let subst = match_pattern_with_expression env pat expr in
            (env, ComputationRedex DoReturn, fun () -> substitute subst comp2')
            :: comps1'
        | Ast.Perform (op, expr, (pat, cont)) ->
            ( env,
              ComputationRedex DoOp,
              fun () ->
                Ast.located at
                  (Ast.Perform
                     (op, expr, (pat, Ast.located at (Ast.Do (cont, comp2)))))
            )
            :: comps1'
        | _ -> comps1')
    | Ast.Delay (n, comp) ->
        let rho = Ast.RhoConst (GS.R.of_nat n) in
        let env' =
          { env with state = ContextHolderModule.add_temp rho env.state }
        in
        [ (env', ComputationRedex Delay, fun () -> comp) ]
    | Ast.Box (rho, expr, (pat, body)) ->
        let rec doBox rho expr (pat : _ Ast.pattern) body =
          match pat.it with
          | Ast.PVar x ->
              let resource_counter = env.resource_counter in
              (* let x' =
              Ast.Variable.fresh
                (Ast.Variable.string_of x ^ string_of_int resource_counter) *)
              let x' =
                Ast.Variable.fresh ("resource_" ^ string_of_int resource_counter)
              in
              let state' =
                ContextHolderModule.add_variable x' (rho, expr) env.state
              in
              let env' =
                {
                  env with
                  state = state';
                  resource_counter = resource_counter + 1;
                }
              in
              [
                ( env',
                  ComputationRedex Box,
                  fun () -> refresh_computation [ (x, x') ] body );
              ]
          | Ast.PAnnotated (pat', _) -> doBox rho expr pat' body
          | _ ->
              Error.runtime "Box expected a variable but got pattern %t"
                (PrettyPrint.print_pattern pat)
        in
        doBox rho expr pat body
    | Ast.Unbox (expr, (pat, body)) ->
        let rec doUnbox (expr : _ Ast.expression) pat body =
          match expr.it with
          | Ast.Var x ->
              let _rho', expr' =
                ContextHolderModule.find_variable x env.state
              in
              let subst = match_pattern_with_expression env pat expr' in
              [ (env, ComputationRedex Unbox, fun () -> substitute subst body) ]
          | Ast.Annotated (expr', _) -> doUnbox expr' pat body
          | _ ->
              Error.runtime "Unbox expected a variable but got expression %t"
                (PrettyPrint.print_expression (module GS.R) expr)
        in
        doUnbox expr pat body
    | Ast.Perform _ -> []
    (* (op, _expr, (_pat, _comp)) ->
      Error.runtime "Unhandled operation %t" (Ast.OpName.print op) *)
    | Ast.Handle (body, handler) -> (
        let comps' =
          step_in_context step_computation env
            (fun red -> HandleCtx red)
            (fun body' -> Ast.located at (Ast.Handle (body', handler)))
            body
        in
        let (pat, ret_comp), op_cases = eval_handler env handler in
        match body.it with
        | Ast.Return expr ->
            let subst = match_pattern_with_expression env pat expr in
            ( env,
              ComputationRedex HandleReturn,
              fun () -> substitute subst ret_comp )
            :: comps'
        | Ast.Perform (op, expr, (op_pat, op_cont)) -> (
            let op_case = Ast.OpNameMap.find_opt op op_cases in
            match op_case with
            | Some ({ it = Ast.PTuple [ op_arg_pat; op_cont_pat ]; _ }, op_case)
              -> (
                let op_sig = Ast.OpNameMap.find_opt op env.op_signatures in
                match op_sig with
                | Some eps ->
                    let resource_counter = env.resource_counter in
                    let x =
                      Ast.Variable.fresh
                        ("resource_" ^ string_of_int resource_counter)
                    in
                    let env' =
                      { env with resource_counter = resource_counter + 1 }
                    in
                    let arg_subst =
                      match_pattern_with_expression env' op_arg_pat expr
                    in
                    let cont_subst =
                      match_pattern_with_expression env' op_cont_pat
                        (Ast.located at (Ast.Var x))
                    in
                    (* The continuation is boxed at the resource grade the
                       operation's effect grade maps to. *)
                    ( env',
                      ComputationRedex HandleOp,
                      fun () ->
                        Ast.located at
                          (Ast.Box
                             ( rho_of_eps eps,
                               Ast.located at
                                 (Ast.Lambda
                                    ( op_pat,
                                      Ast.located at
                                        (Ast.Handle (op_cont, handler)) )),
                               ( Ast.located at (Ast.PVar x),
                                 substitute cont_subst
                                   (substitute arg_subst op_case) ) )) )
                    :: comps'
                | None ->
                    Error.runtime
                      "TODO: Operation signature not found in runtime state")
            | Some _ ->
                Error.runtime "TODO: Operation case not in correct format"
            | _ ->
                ( env,
                  ComputationRedex HandleOp,
                  fun () ->
                    Ast.located at
                      (Ast.Perform
                         ( op,
                           expr,
                           ( op_pat,
                             Ast.located at (Ast.Handle (op_cont, handler)) ) ))
                )
                :: comps')
        | _ -> comps')

  type load_state = {
    environment : evaluation_environment;
    computations : Graded.computation list;
  }

  let initial_load_state =
    { environment = initial_environment; computations = [] }

  let load_primitive load_state x prim =
    {
      load_state with
      environment =
        {
          load_state.environment with
          builtin_functions =
            ContextHolderModule.add_variable x
              (P.primitive_function prim)
              load_state.environment.builtin_functions;
        };
    }

  let load_ty_def load_state _ = load_state

  let load_top_let load_state x expr =
    {
      load_state with
      environment =
        {
          load_state.environment with
          variables =
            ContextHolderModule.add_variable x expr
              load_state.environment.variables;
        };
    }

  let load_top_do load_state comp =
    { load_state with computations = load_state.computations @ [ comp ] }

  let load_op_sig load_state op eps =
    {
      load_state with
      environment =
        {
          load_state.environment with
          op_signatures =
            Ast.OpNameMap.add op eps load_state.environment.op_signatures;
        };
    }

  let load_op_default load_state op abs =
    {
      load_state with
      environment =
        {
          load_state.environment with
          op_defaults =
            Ast.OpNameMap.add op abs load_state.environment.op_defaults;
        };
    }

  type run_state = load_state
  type step_label = ComputationReduction of computation_reduction | Return

  type step = {
    environment : evaluation_environment;
    label : step_label;
    next_state : unit -> run_state;
  }

  let run load_state = load_state

  let steps = function
    | { computations = []; _ } -> []
    | { computations = { it = Ast.Return _; _ } :: comps; environment } ->
        [
          {
            environment;
            label = Return;
            next_state =
              (fun () ->
                (* Reset resource state between consecutive top-level [run]
                   commands: top-level bindings and operation signatures stay,
                   but the resource store and fresh-resource counter start
                   from zero again. *)
                let environment' =
                  {
                    environment with
                    state = ContextHolderModule.empty;
                    resource_counter = 0;
                  }
                in
                { computations = comps; environment = environment' });
          };
        ]
    (* A default implementation fires only here, where the operation call has
       bubbled out of every enclosing [do] and [handle] and so is known to be
       unhandled; [step_computation] deliberately gets no [Perform] rule. The
       body runs in place of the call and its result is passed to the
       continuation, exactly as a handled operation's result would be. *)
    | {
        computations = { it = Ast.Perform (op, expr, (pat, cont)); at } :: comps;
        environment;
      }
      when Ast.OpNameMap.mem op environment.op_defaults ->
        let dpat, dcomp = Ast.OpNameMap.find op environment.op_defaults in
        let dpat', vars = refresh_pattern dpat in
        let dcomp' = refresh_computation vars dcomp in
        let subst = match_pattern_with_expression environment dpat' expr in
        [
          {
            environment;
            label = ComputationReduction (ComputationRedex DefaultOp);
            next_state =
              (fun () ->
                {
                  computations =
                    Ast.located at
                      (Ast.Do (substitute subst dcomp', (pat, cont)))
                    :: comps;
                  environment;
                });
          };
        ]
    | { computations = comp :: comps; environment } ->
        List.map
          (fun (env, red, comp') ->
            {
              environment = env;
              label = ComputationReduction red;
              next_state =
                (fun () ->
                  { computations = comp' () :: comps; environment = env });
            })
          (step_computation environment comp)
end
